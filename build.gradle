import org.ajoberstar.gradle.git.release.semver.*
import org.ajoberstar.gradle.git.release.opinion.Strategies

plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'de.gliderpilot.semantic-release' version '1.4.2'
    id 'java'
    id 'application'
}

group = 'com.example'
sourceCompatibility = '1.8'
mainClassName = "com.example.demo.DemoApplication"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    sourcesJar
}

project.ext.ghToken = project.hasProperty('ghToken') ? project.getProperty('ghToken') : System.getenv('GH_TOKEN') ?: null

release {
    // replace the default strategy to add '-FINAL' to the version
    versionStrategy semanticRelease.releaseStrategy.copyWith{
        preReleaseStrategy: { it.copyWith(inferredPreRelease: 'FINAL') }
    }

    // add a second strategy to create release candidates from 'main/.*' branches
    versionStrategy semanticRelease.releaseStrategy.copyWith(
            // the type is important, without type you would again replace the default strategy
            type: 'main',
            selector: { SemVerStrategyState state ->
                !state.repoDirty && state.currentBranch.name ==~ /main\/.*/ &&
                        semanticRelease.semanticStrategy.canRelease(state) && project.gradle.startParameter.taskNames.find { it == 'release' }
            },
            preReleaseStrategy: StrategyUtil.all({ it.copyWith(inferredPreRelease: 'main') } as PartialSemVerStrategy, Strategies.PreRelease.COUNT_INCREMENTED)
    )
}

semanticRelease {
    repo {
        ghToken = project.ghToken
        releaseAsset jar, contentType: 'application/zip'
        releaseAsset sourcesJar, label: 'the sources of the project', contentType: 'application/zip'
    }
}

jar {
    manifest {
        attributes([
                "main-class": mainClassName
        ])
    }
}
afterEvaluate {
    println version
}
